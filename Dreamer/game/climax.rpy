#절정 파트 이야기 => 열쇠 잡는 여정.
init:
    image bg_snj_climax = "climax/bg_snj_climax.png"
    image bg_desert = "climax/bg_desert.png"
    image bg_key = "climax/bg_key.png"
    image bg_key_light = "climax/bg_key_light.png"
    image bg_hut_light = "climax/bg_hut_light.png"
    image bg_sign = "climax/bg_sign.png"
    image bg_hut = "climax/bg_hut.png"

label climax_start_good_a :
    scene white with dissolve
    play music "audio/Climax.mp3" fadeout 1

    "빛이 강하다. 눈을 뜨기가 어려워."
    "열기가 느껴진다. 여긴..."

    scene bg_desert with dissolve
    show t serious at center with dissolve
    t "아니 여긴.. 사막 아닌가요...?"
    show s surprise at centerleft with dissolve
    s "사막...? 이곳도 누군가의 방인가..?"
    t "그런 것 같지는 않아요.. 아마 열쇠가 있는 곳이겠죠.."

    "아니..아니지.. 여기가 어디인지가 중요한 게 아니야 지금"
    "열쇠를 한 사람만 잡을 수 있다는 게 도대체..."

    show j worry at centerright with dissolve
    j "얼음성에 무슨 사막이.. 하여튼 알 수 없다니까 여긴."

    show s normal
    "한 명만 이곳을 나갈 수 있다고..?"

    j "S, 괜찮아? 어디 다친 데 없어?"
    if a_room_check == True:
        jump climax_start_good_a_2

    j "A는...A는 어떻게 됐어...?"

    "A... 그래서 A의 정체가 뭔데...? 이 세계를 만든 게 A라고? 우리를 가둔 거야? 왜..?"

    j "S! 얼 빠진 사람처럼 왜 그래! 어디 다친 거야?"

    "우리가 나가면 이 세계가 무너져...? 그럼 이곳에 있는 사람들은..?"

    t talk "S씨!!"
    s surprise "어..? 어, 미안해요. 무슨 말 했었죠?"
    j "어디 다친 곳 없어?"
    s "어... 괜찮은 것 같아."
    t "A씨는..."
    s normal "A는..."

    "A가 어떻게 되었다고 말해야 하는 거지...? A가 알고 보니 이 세계의 설계자였다고...?"
    "아니.. 아니 그건 조금 생각을 정리하고 말하자."

    s talk "A는... 빠져나오지 못했어..자세한 건.. 나도 잘.."
    j lonely "아..."
    t serious "A씨 결국... 아이를 두고 오지 못 했네요..."
    j "A..."
    s normal "..."

    "일단 둘러댔지만.. 사실을 말해야 하지 않을까..?"

    t "일단 걸어 가 볼까요...?"
    j normal "...좋아요."

    scene bg_desert with dissolve
    show s normal:
        xzoom -1
        xalign 1.1
        yalign 1.0
    show j lonely at center with dissolve
    "J의 표정이 어둡다."
    "아마 조금 더 설득했더라면 하고 자책하고 있는 것 같다."
    "J는 A와 꽤나 오래 알고 지낸 사이인데..."
    "A의 정체를 J도 몰랐겠지..?"

    show t talk at left with dissolve
    t "저기 뭔가가 보이네요!"

    "열쇠에 대해 진실을 말하면 내가 이곳을 빠져나가지 못할 수도 있겠지..? 그동안 이 세계가 무너지면...? 그럼 영영 이곳에 갇히는 거 아닌가...?"

    t "S씨, T씨 이 쪽이에요! 어서 오세요!"
    hide t with moveoutleft
    j "네..!"
    hide j with moveoutleft

    "너무 이기적인 생각만 하고 있네... 어쩌지.."

    scene bg_sign with dissolve

    show t serious at center with dissolve
    t "여기 표지판이 있는데.. 적힌 말이.."
    show j worry at centerright with dissolve
    j "이 표지판 근방 3미터 안에 누군가가 머물러야 열쇠가 있는 오두막의 입구가 열립니다...?"
    t "한 사람은 여기 남아서 기다려야 한다는 건가 봐요."

    show s normal at centerleft with dissolve
    "잠...잠깐, 그렇다면.."

    j "어떻게 하죠..?"
    t "아무래도 제가 남는 게 맞지 싶어요."
    j "혼자서... 괜찮겠어요?"
    t "제가 남는게 좋을 것 같아요. 저는 이곳에 오래 머물기도 했고.. 아마 어떤 상황이 생겨도 잘 대처할 수 있을 테니까요."

    show s surprise
    "열쇠를 한 명만 잡을 수 있다면... 여기 남는 사람은 애초에 탈출할 기회조차 없는 거잖아...!"

    t talk "무엇보다도 S씨는 밀실에서 나온 지 얼마 안되기도 하셨고.. J씨랑 함께 가시는 게 좋을 것 같아요!"
    t smile "제 걱정은 말아요. 두 분께서 열쇠를 찾으신 다음에 절 잊지 마시고 이곳에 와 주시기만 하면 돼요. 허허."

    show s normal
    "아니... 이곳에 있으면 열쇠를 죽었다 깨어나도 구경 못해요... 이 곳에 남는다면...!"

    j lonely "음.. T씨께서 그렇게 말씀해 주신다면 너무 감사하죠. S, 너는 어때?"

    "어쩌지...? 말을 해야 하나...?"
    "말을 했다가 내가 못 나가게 되면...? 그렇다면 어떻게 해...?"
    "눈 딱 감고 모르는 척할까? 어차피 만난 지 얼마 안되는 사람인데..."
    "아니 그래도 어떻게 사람이 돼서..."
    "아아악 미치겠네 어떻게 하지...?"

    j worry "S?"

    "그래... 그래 이렇게 하자"
    menu:
        "말한다":
            s talk "사실..."
            "T, J" "?"
            s "사실 A가 어떤 말을 했어..."
            s "열쇠를 한 사람만 잡을 수 있다고.. 그니까 그 말은.."
            s "한 사람만 이 곳을 나갈 수 있다는 거지.."
            j lonely "....뭐?"
            t serious "네? 그게 무슨 말이에요..? 열쇠를 한 사람만 잡을 수 있다고요?"
            j "아니, 아니 잠깐만. A가 그런 말을 했다고...?"
            s "응.. A가 사라지기 전에 나한테 말했어.."
            t "그게 사실인가요? 그게 사실이라면 왜 A씨는 진작에 그걸 말해주지 않은 거죠...?"
            s surprise "저도 간략하게 들어서 뒤죽박죽인데..."
            s "A가 이 꿈의 세계를 만든 사람이고... 그리고 이 세계가 유지되기 위해서는 우리 같은 사람이 필요하다고 했어요."
            s "그게 무슨 말인지는 저도 잘 이해가 되지 않아요.."
            j worry "잠깐...잠깐 그러니까 A가... 설계자라고...? 이 세계의...?"
            s "그런 것 같아.."
            t talk "그게 무슨.."
            s "받아들이기 힘들지만 지금으로서는 우리가 정확한 진실을 알 방도가 없는 것 같아.."
            s "아무튼 A는 열쇠를 한 사람만 잡을 수 있다고 말했어.."
            show j scream
            j "왜 진작에 이야기하지 않은 거야 S?"
            s talk "미안해.. 나도 정말 혼란스러워서.."
            t "어쩌죠... 이제...? 이곳에 누군가가 남아야 열쇠를 찾을 수 있을 텐데.."
            j lonely "하지만 그 사람은.."
            s "열쇠를 잡지 못하겠지... 그리고 오두막을 간 사람이라도 한 명은..."
            t "아.."

            jump climax_good_said

        "말하지 않는다":

            jump climax_good_not_said


label climax_start_good_a_2 :
    j "A는... A가 이곳의 설계자라는게 도대체 무슨 말일까...?"
    t "이곳을 만든게 정말로 A씨일까요...? 그럼 우리를 이곳에 묶어 두는 것도 A씨라는 말인가요...?"

    "A가 그랬는데.. 우리가 나가면 이 세계가 무너진다고.. 그럼 이곳에 있는 사람들은...?"

    j "S! 얼 빠진 사람처럼 왜 그래! 어디 다친 거야?"
    t talk "S씨!!"
    s surprise "어..? 어, 미안해요. 무슨 말 했었죠?"
    j "어디 다친 곳 없어?"
    s "어.. 괜찮은 것 같아."
    t "혹시 A씨에 대해서는..."
    s normal "A는..."
    s talk "A가 말했어.. 자기가 이 세계를 만들었고.. 우리가 있어야 이 세계가 돌아간다고... 우리가 없으면 이 세계가 붕괴될 거라고..."
    t "붕괴요...?"
    s "자세한 건 저도 잘 모르겠어요. 너무 순식간에 일어난 일이라..."
    j lonely "하..여태 A가 누구인지도 모르고 지내왔다니.."
    t serious "그럼 얼음성을 만든 것도.. 우리가 거쳐온 그 모든 방을 만든 것도 A씨라는 걸까요..?"
    s normal "A의 방이 A의 의지대로 만들어지지는 않은 것 같던데..."
    j worry "모든 게 엉망이야... 이제 정말 아무것도 모르겠다고!"
    t talk "J씨, 우리 침착해져요... 저도 정말 놀랐지만... 지금은 일단 열쇠를 찾는게 급선무니까요..!"
    j lonely "하..."

    show s normal
    "열쇠...맞다 열쇠. 열쇠를 한 명만 잡을 수 있다는 걸 어떻게 말하지...? 아니... 말을 해야 하나...? 말을 했다가 내가 못나가게 되면...?"
    "말을 안하고 그냥 내가 먼저 잡으면 여길 나갈 수 있는 거잖아..!"

    t "일단 걸어 가 볼까요...?"
    j normal "...좋아요."

    scene bg_desert with dissolve
    show s normal:
        xzoom -1
        xalign 1.1
        yalign 1.0
    show j lonely at center with dissolve
    "J의 표정이 어둡다."
    "A가 이 모든 공간을 만든 설계자라는 걸 받아들이고 굉장한 충격을 받은 모양이다."
    "J는 A와 꽤나 오래 알고 지낸 사이인데..."
    "A의 정체를 J도 몰랐구나..?"

    show t talk at left with dissolve
    t "저기 뭔가가 보이네요!"

    "열쇠에 대해 진실을 말하면 내가 이곳을 빠져나가지 못할 수도 있고.. 그동안 이 세계가 무너지면...? 그럼 영영 이곳에 갇히는 거 아닌가...?"

    t "S씨, T씨 이 쪽이에요! 어서 오세요!"
    hide t with moveoutleft
    j "네..!"
    hide j with moveoutleft

    "너무 이기적인 생각만 하고 있네... 어쩌지.."

    scene bg_sign with dissolve

    show t serious at center with dissolve
    t "여기 표지판이 있는데.. 적힌 말이.."
    show j worry at centerright with dissolve
    j "이 표지판 근방 3미터 안에 누군가가 머물러야 열쇠가 있는 오두막의 입구가 열립니다...?"
    t "한 사람은 여기 남아서 기다려야 한다는 건가 봐요."

    show s normal at centerleft with dissolve
    "잠...잠깐, 그렇다면.."

    j "어떻게 하죠..?"
    t "아무래도 제가 남는 게 맞지 싶어요."
    j "혼자서... 괜찮겠어요?"
    t "제가 남는게 좋을 것 같아요. 저는 이곳에 오래 머물기도 했고.. 아마 어떤 상황이 생겨도 잘 대처할 수 있을 테니까요."

    show s surprise
    "열쇠를 한 명만 잡을 수 있다면... 여기 남는 사람은 애초에 탈출할 기회조차 없는 거잖아...!"

    t talk "무엇보다도 S씨는 밀실에서 나온 지 얼마 안되기도 하셨고.. J씨랑 함께 가시는 게 좋을 것 같아요!"
    t smile "제 걱정은 말아요. 두 분께서 열쇠를 찾으신 다음에 절 잊지 마시고 이곳에 와 주시기만 하면 돼요. 허허."

    show s normal
    "아니... 이곳에 있으면 열쇠를 죽었다 깨어나도 구경 못해요... 이 곳에 남는다면...!"

    j lonely "음.. T씨께서 그렇게 말씀해 주신다면 너무 감사하죠. S, 너는 어때?"

    "어쩌지...? 말을 해야 하나...?"
    "말을 했다가 내가 못 나가게 되면...? 그렇다면 어떻게 해...?"
    "눈 딱 감고 모르는 척할까? 어차피 만난 지 얼마 안되는 사람인데..."
    "아니 그래도 어떻게 사람이 돼서..."
    "아아악 미치겠네 어떻게 하지...?"

    j worry "S?"

    "그래... 그래 이렇게 하자"
    menu:
        "말한다":
            s talk "사실..."
            "T, J" "?"
            s "사실 A가 어떤 말을 했어..."
            s "열쇠를 한 사람만 잡을 수 있다고.. 그니까 그 말은.."
            s "한 사람만 이 곳을 나갈 수 있다는 거지.."
            j lonely "....뭐?"
            t serious "네? 그게 무슨 말이에요..? 열쇠를 한 사람만 잡을 수 있다고요?"
            j "아니, 아니 잠깐만. A가 그런 말을 했다고...?"
            s "응.. A가 사라지기 전에 나한테 말했어.."
            t "그게 사실인가요? 그게 사실이라면 왜 A씨는 진작에 그걸 말해주지 않은 거죠...?"
            s surprise "저도 간략하게 들어서 뒤죽박죽인데..."
            s "아까 말했듯이 A가 이 꿈의 세계를 만든 사람이고... 그리고 이 세계가 유지되기 위해서는 우리 같은 사람이 필요하다고 했어요."
            s "그게 무슨 말인지는 저도 잘 이해가 되지 않아요.."
            s "그냥 A는 우리가 이곳에 오는 걸 실패하기를 바랐던 거 같기도 해요. 마지막엔 저를 보내주었지만..."
            s "아무튼 A는 열쇠를 한 사람만 잡을 수 있다고 말했어요.."
            j scream "왜 진작에 이야기하지 않은 거야 S?"
            s talk "미안해.. 나도 정말 혼란스러워서.."
            t "어쩌죠... 이제...? 이곳에 누군가가 남아야 열쇠를 찾을 수 있을 텐데.."
            j lonely "하지만 그 사람은.."
            s "열쇠를 잡지 못하겠지... 그리고 오두막을 간 사람이라도 한 명은..."
            t serious "아.."

            jump climax_good_said

        "말하지 않는다":

            jump climax_good_not_said

label climax_good_said:
    show s normal
    "심장박동 소리가 새벽의 사이렌 소리 마냥 귓전을 때린다. 무서운 정적이다."
    "모두가 굳은 얼굴로 입을 다물고 있다."
    "나는... 나는 이곳을 나가고 싶어... 여기에 남을 용기가 없는데.."
    "사막이 이렇게 조용한 공간이었던가...? 숨이 막힌다."
    "다들 어떤 생각을 하고 있는 걸까?"

    t serious "제가.."
    s "?"
    t "제가 남을게요."
    j worry "네...?"
    show s surprise
    t "저는 어차피 이 꿈에서 깨어나도.. 제가 일했던 그 저택에 다시 돌아갈 수는 없으니까요."
    t "이전 주인님도 안 계시고.."
    t "그리고 어렴풋이 알고 있었어요. 이곳에서 나가도 아마 현실에선... 상당한 시간이 흘렀을 거라는 걸."
    t "아마 나간다고 하더라도 큰 의미가 없겠죠..."
    t "그리고... 아까 그 방에서 본 그 저택.."
    t "방 문이 닫히기 전에 정원에 다시 햇살이 비친 것 같다고 말했었죠.."
    t "그 모습이 아직 아른거려요."
    t "저는 어쩌면 예전부터 이 곳을 탈출하기보다 이 꿈 속의 저택에서 남은 시간을 보내기를 원하고 있었을지도 몰라요."
    t "아니, 그렇게 생각해요."
    t "그러니까... 제가 남을게요. 전 정말 괜찮아요."
    s talk "T씨... 정말로..."
    j "정말 괜찮으신 거예요...? 뭐라 말을 해야할지 모르겠어요.. 정말... 이 세계를 나갈 수 없어도 괜찮으세요...?"
    t "괜찮아요. 정말 괜찮으니까 두 분께선 어서 오두막으로 가세요."
    s "T씨..."
    j normal "정말... 고마워요 T씨.. 덕분에 이곳까지 올 수 있었어요. 정말 너무 고마워요.."
    s "고마워요... 꼭... 저택에서 행복한 시간을 보낼 수 있기를 바랄게요.. 정말 고마워요.."
    t smile "저는 걱정 마시고 어서 가세요..! 두 분은 부디 꼭... 기적이 일어나서 모두 이곳을 탈출하시기를 바랄게요."
    j "그럼.. 가볼게요.. 몸 조심해요 T..!"
    s "그동안 고마웠어요..!"
    t "두 분도 몸 조심하세요..!"

    scene black with dissolve
    "결국 T가 남게 되었다. 뭐라 표현하기 힘든 부채감이 느껴진다."
    "살면서 이렇게 끔찍한 고민을 한 적이 있던가...?"

    scene bg_desert with dissolve
    show s normal:
        xzoom -1
        xalign 0.25
        yalign 1.0
    show j lonely at centerright
    "J와 표지판이 가리키는 방향으로 걷는 중이다."
    "J도 죄책감을 느끼고 있는 듯하다. 우리 둘 다 말을 안 하고 걷기만 한 지 꽤 되었다."

    if j_para >= 1:
        j standup "말해줘서 고마워."
        show s surprise:
            xzoom 1
        with dissolve
        s "어...?"
        j "열쇠를 한 사람만 잡을 수 있다는 거. 말해줘서 고마워."
        s talk "아니 나는 오히려... 너무 미안해. 바로 말해줬어야 했는데.."
        s "솔직히 조금... 조금 고민을 했어. 정말 미안해 정말 이기적인 생각이었는데.."
        j normal "아니야 나였어도 엄청 고민했을 거야. 어쩌면 말을 못했을 수도 있을 거 같아."
        j "말해줘서 고마워 S."
        s "응..."

        show s normal:
            xzoom -1
            xalign 0.15
            yalign 1.0
        with move
        "J는 오히려 나를 위로해 준다."
        "내가 말하지 않는 선택을 할 뻔했다는 걸 알고 있음에도..."

        j "저기 오두막이다!"
        show s talk:
            xzoom -1
        s "어, 정말..! 드디어.. 온 건가...?"
        j "어떤 일이 생길 지 모르니까 긴장하자."

    else:
        j worry "어떻게 말을..."
        show s surprise:
            xzoom 1
        with dissolve
        s "어...?"
        j lonely "아니다... 아니야. 그냥..."
        j worry "일찍 알려줬다면..."
        j lonely "아니야."
        s normal "..."

        show s normal:
            xzoom -1
            xalign 0.10
            yalign 1.0
        with move
        "J는 나에게 실망한 눈치다."
        "그래.. 나였어도 실망했겠지.."

        j "어... 저기 오두막이 보여."
        show s talk:
            xzoom -1
        s "드디어.. 온 건가...?"
        j "..."
        j "어떤 일이 생길 지 모르니까 긴장하자."
        s "그래..."

    "드디어 열쇠가 있는 곳에 다다랐다."
    "이렇게 전혀 기쁨을 느끼지 못하는 상황이 될 줄은 정말 몰랐는데..."

    j lonely "문 열어 볼게...?"
    s "그래. 같이 열자."

    scene black with dissolve
    pause .3
    scene bg_hut with dissolve

    show s surprise at centerleft with dissolve
    s "생각보다..."
    show j worry at centerright with dissolve
    j "텅 비었네..? 건초만 흩뿌려져 있고..."
    s "저기 저거, 말구유인가?"
    j "그런 거 같아. 다가가도 되겠지...?"
    s talk "조심해서 가보자."

    show s normal
    "아...잠깐..."
    "잊고 있던 A의 말이 생각났다."
    "우리가 이곳을 떠난다면 이 세계와 자신의 아이도 모두 소멸한다고 했는데..."
    "여길 빠져나가면.. 이 세계에는 무슨 일이 생기는 거지...?"

    scene bg_key with dissolve
    show j lonely at centerright
    show s talk at centerleft
    j "S, 여기 열쇠가 있어."
    s "아니.. 이게 열쇠라고...? 완전 녹이 슨 고철덩이잖아...?"
    j "그래도 완전한 열쇠 모양인 걸 보니 이게 그 열쇠가 맞는 거 같아."
    s surprise "아... 드디어..."

    show j normal
    show s normal
    "우리 둘 다 기뻐하지는 않았다. 누군가는 저 열쇠를 잡을 수 없다는 알기 때문이겠지."

    "어떻게 할까.. 어떻게 해야 하는 걸까..."
    "이곳을 나가고 싶다.. 하지만... J를 버리고 나만 나가는 건..."
    "어쩌지..? 어떻게 해야 하는 거야..."
    "J도 고민하는 눈치인데...내가 생각하는 사이에 저 열쇠를 먼저 잡으면 어떻게 하지...?"
    "난 그대로 이곳에 영영 갇히는 건가..?"
    "그럼... 그럼 내가 먼저 저 열쇠를 차지해...?"
    "어떻게 하든 후회가 남겠지... 결정을 해 S... 어떻게 할거야...!"

    menu:
        "J에게 양보하자":
            scene bg_hut with dissolve
            show bg_snj_climax
            s "J..."
            j "..."
            s "J!"
            j "...어! 미안 생각 좀 하느라..."
            s "음... 저 열쇠... 네가 잡아. 나는 괜찮아."
            j "어?"
            s "생각해 보았는데.. 네가 이곳에 더 오래 머물기도 했고... 네가 잡는 게 맞는 거 같아."
            j "하지만 S 그러면 내가 너무..."
            s "아니야 괜찮아. 얼마든지 다른 기회가 있고.. 나는 열쇠가 있는 이곳까지 온 사람이니까 다음에도 잘하겠지!"
            s "괜찮아 J, 내 걱정 말고 얼른 열쇠 잡아."
            j "S... 나는... 내가.."
            s "J, 나는 너를 만나지 않았더라면 지금쯤 네가 말한 그 사냥꾼에게 잡혔을 수도 있어.. 너에게 신세진 게 많아."
            s "그러니까... 네가 열쇠를 잡아. 나는 정말 괜찮아."
            j "S..."

            scene black with dissolve
            "이 세계가 언제 붕괴될 지 모르니까 불안하지만... 그래도 J를 보내주는 게 맞는 거 같아."
            "잘 선택한 거야 S, 너다운 선택을 한 거야.."

            scene bg_hut with dissolve
            show bg_snj_climax
            show j normal at centerright with dissolve
            j "고마워... 정말..."
            show s relax at centerleft with dissolve
            s "만나서 정말 반가웠어.. 꼭 이곳을 탈출해서 다시 만나자 J.."
            j "그래 꼭.."
            s "자, 어서 열쇠 잡아! 나는 이곳에서 너 가는 것까지 보고 T에게 갈게."
            j "정말 고마워 S... 정말 고마웠어.."
            j "S, 잊지 않을게...!"

            "J가 열쇠를 쥔다. 아주 먼 나중에 이 선택을 후회할 수도 있겠지만..."
            "그래도 괜찮아..."

            scene bg_key_light with dissolve
            "아.. J의 몸이 뜬다. J는 이미 눈을 감고 있다. 무슨 생각을 하고 있을까...? 아니, 잠에 든 것 같기도.."
            "나도 졸음이 몰려온다... 뭘까... 나는 이제 어떻게 되는 걸까...?"

            jump end_true_1

        "먼저 열쇠를 차지하자":
            show s surprise
            "안 되겠어... 역시 나는 나가야 해...!"
            "J 미안해... 하지만 어쩔 수 없어. 내가 나가야 하니까..."

            show s surprise at center with move
            show s surprise at center with hpunch
            show j worry at right with move
            pause .5

            scene bg_key_light with dissolve
            show s give_up at center
            show j breathe at right
            j "....!!"
            s "미안해, 어쩔 수 없었어. 정말 미안해!!"
            j scream "너!!!"

            "J를 밀치고 열쇠를 잡았다. 급하게 잡느라 까슬까슬한 표면에 손바닥이 찢긴 것 같다."
            "J를 쳐다보지 못하겠다. 나는.. 나는 쓰레기다."
            "그래, 나는 내가 수거하는 쓰레기만도 못한 인간이다."
            "J가 황망히 서있다. 저 눈과 마주치면 평생 못 잊을 거 같아 눈을 질끈 감았다."

            scene bg_hut_light
            "몸이 뜨기 시작한다. 그리고 졸음이 몰려온다..."
            "J의 외침도 더 이상 들리지 않는다. 이제 정말 끝인가..."

            jump end_true_2

label climax_good_not_said:
    s surprise "..."

    "내가 살려면.. 그래 내가 살려면 어쩔 수 없는 거야..."

    s talk "T씨께서 그렇게 해주신다면... 너무 감사하죠...!"
    t normal "저는 그럼 여기에 있을게요!"
    t "두 분께선 어서 오두막으로 가세요!"
    j normal "네! 열쇠 가지고 금방 올게요!"
    s normal "..."
    j "그럼.. 가볼게요! 몸 조심해요!"

    "괴롭다. T의 얼굴을 볼 수가 없다."

    t smile "두 분도 몸 조심하세요..!"

    scene bg_desert with dissolve
    show j normal at centerleft
    show s normal:
        xzoom -1
        xalign 0.85
        yalign 1.0

    "결국 T가 남게 되었다."
    "T는 나 때문에 이곳을 빠져나갈 기회조차 잃었다. 내가 말을 하지 않아서."
    "토가 나올 것 같다. 오아시스가 있다면 머리라도 처박고 모든 걸 포기하고 싶은 심정이다."
    "아니, 혼자 살겠다고 진실을 숨겼으면서 이건 또 무슨 모순되는 생각인 걸까..? 아무튼 죽을 맛이다."
    "살면서 이렇게 끔찍한 고민을 한 적이 있던가...?"
    "J와 표지판이 가리키는 방향으로 걷는 중이다. J는 곧 열쇠를 잡을 수 있다는 사실에 꽤나 신이 난 모양이다."

    j "빨리 찾아서 T씨에게 가자! 너무 감사하다 정말.."
    s "..."
    j "아 지금 나 너무 설레. 나는 나가자마자 할 일도 정했어."
    j "일단 카슈를 닮은 유기견을 입양할 거야. 그리고... 카슈에게 주었던 사랑만큼.. 아니 그것보다 더 많이 사랑해주고 아껴 줄거야.."
    j "우리 카슈도 동생 생긴 걸 엄청 기뻐해 주겠지...?"

    "J가 부럽다. 차라리 열쇠에 대한 비밀을 알지 못했다면 조금 나았을까..?"

    j "새로운 아이랑 같이 카슈의 무덤도 갈거야."

    show s surprise
    "이제..어쩌지...?"
    "오두막에 도착해서 열쇠가 보이면... 바로 달려가야 하나..?"
    show s normal
    "A가 나를 보내주기는 했지만... 우리 같은 사람이 나갈수록 이 세계가 위태로워진다는 그 말은..."
    "만약 내가 나가서 여기가 붕괴된다면...? 그럼 남은 사람들은..?"
    "아니, 아니지 J가 나가도 그건 마찬가지잖아. 그럼... 그럼 내가 나가는 게.."

    j worry "S!"
    s surprise "어..어..!"
    j "아까부터 뭘 그렇게 생각하고 있어? 불러도 대답도 않고."
    s "미안해 그냥.."
    s talk "그냥 조금 뒤숭숭해서."
    j "하긴. 우리가 곧 이곳을 나가게 될 거라고 생각하니까 조금 이상하기는 하다."
    j "어?"
    s "?"
    j "S, 저기 오두막이야!"
    hide j with moveoutleft

    show s normal
    "어, 정말..! 드디어.. 온 건가...?"

    scene bg_desert with dissolve
    show j normal at centerright
    show s normal at centerleft
    j "혹시 모르니까 긴장 늦추지 말자."
    s "그래."

    "드디어 열쇠가 있는 곳에 다다랐다."
    "이렇게 전혀 기쁨을 느끼지 못하는 상황이 될 줄은 정말 몰랐는데..."

    j "문 열어 볼게...?"
    s "그래. 같이 열자."

    scene bg_hut with dissolve

    show s surprise at centerleft with dissolve
    s "생각보다..."
    show j lonely at centerright with dissolve
    j "텅 비었네..? 건초만 흩뿌려져 있고..."
    s talk "저기 저거, 말구유인가?"
    j "그런 거 같아. 다가가도 되겠지...?"
    s "조심해서 가보자."

    "아...잠깐...만약 저기에 열쇠가 있다면...!"

    show j normal at center with move
    j "S, 여기 뭔가가 있어!!"
    s surprise "자..잠깐! 나도 볼게!"
    j "녹이 엄청 슬었지만 분명 열쇠다!! 열쇠야!!"

    "어? J가 열쇠를 잡으려고 한다..!!"

    s "안돼!!!"

    show s surprise at center with move
    show s surprise at center with hpunch
    show j worry at right with move
    pause .5

    j breathe "악!"
    s "아..."
    j scream "뭐야 S? 왜 밀치는 거야?!!"

    "이..일단 밀쳐버렸는데.. 어쩌지..? 진실을 얘기하고 차분하게 대화를 해야 하나..?"
    "아니면 대충 둘러대다가 먼저 잡아버릴까...?"

    menu:
        "지금이라도 솔직하게 말하자":
            s "그...그게 말이야 J.."
            scene bg_key with dissolve
            show s talk at centerleft with dissolve
            s "이 열쇠..."
            show j scream at right with dissolve
            j "왜 그래, 뭔데?"
            s "딱 한 사람만 잡을 수 있어.."
            j lonely "뭐..?"
            s "그 방에서 나오기 전에 A가 말했어.. 열쇠는 단 한 명만 잡을 수 있다고.."
            j "한 명만 잡을 수 있다는 게 무슨 소리야? 여길 한 사람만 나갈 수 있다는 거야?"
            s normal "...그런 거 같아.."
            j scream "왜 그걸 이제야...!"
            j "왜 진작에 말하지 않은 거야?"
            s talk "미안해 정말.. 정말 미안해 J.. 너무 혼란스러워서 그랬어.. 정말 미안해."
            j worry "S, 어떻게... 그걸 여태껏 혼자만..."
            s "정말... 정말 미안해..."
            s "나도 후회돼.. 하지만... 이미 이렇게 되어버렸으니까..."
            s "어떻게 해야 할지... 생각을 해봐야 할 것 같아.."
            j lonely "우리 둘 중 누가 나갈지를 정하자고..?"
            s "정말 말도 안 되지만..."
            j "하.. 어쩌다 이렇게..."
            s normal "..."
            j "그럼 T씨는...? T는 저 사막에서 우릴 계속 기다리게 만들 셈이야...? 아무 것도 모른 채로..?"
            s talk "그건.."
            s "남게 된 다른 한 사람이.. T씨에게 가야겠지.."
            j "T씨가 이 곳에 올 기회조차 없이...?"
            s surprise "J..J 너도 알잖아.. 너도 이 공간이 얼마나 불안정한지..."
            s "아까 이 오두막으로 오면서.. 분명 옆에 있었던 선인장이 갑자기 사라진다거나.. 우리 키를 넘을 만큼 쌓여 있던 모래가 순식간에 사라지는 모습을 보았어.."
            s "T씨가 있는 곳으로 되돌아 간다면 다음 기회는 없을 거야."
            s "그건 확실해..."
            j "..."
            j tear "어쩌다 이렇게.."
            j "..."
            s normal "..."

            scene black with dissolve
            pause .5
            scene bg_desert with dissolve
            show bg_snj_climax

            "한참을 우리 둘 모두 말이 없었다. 우리는 열쇠에서 조금 떨어진 오두막 모퉁이에 앉았고, 그리고 나는..."
            "나는 그냥 열쇠를 잡아버릴 걸 그랬나..하는 생각을 하다가도 퍼뜩 놀란다."
            "내가 이런 사람이었나...?"
            "괴롭다. 뾰족한 수가 떠오르지 않는다. 한 사람이 양보하지 않는 이상... 애초에 해결책이 있는 상황이 아니니까..."

            j "더 이상 결정을 늦추는 것도 위험하겠지..."
            s "..."
            j "나도 정말 모르겠어. 그래서 어이없는 결론이 났어."
            s "그게 뭔데...?"
            j "그냥 운에 맡기는 거야."
            s "운...?"
            j "동시에 잡자."
            s "동시에..?"
            j "3,2,1을 동시에 외치면서 ‘1’에 저 열쇠를 함께 잡는 거야."
            j "누가 먼저 잡는지는.. 운에 달렸지."
            s "하지만 그건..."
            j "S, 나는 아직 너를 믿으니까.. 그러니까 이 제안을 하는 거야.."
            j "그리고 무엇보다도.. 정말 우리가 동시에 잡았다면.. 함께 탈출할 수도 있는 거 아니겠어?"

            "그러고 보니 충분히 가능성이 있다."
            "영화에서 보면 저런 반전도 있잖아...? 동시에 잡았더니 함께 탈출할 수 있었더라 뭐 이런..."
            "처음엔 말도 안되는 방식이라고 생각했는데.. 나쁘지 않은 것 같다."

            s "그럼.. 열쇠 앞에서 3,2,1을 외치고 저 열쇠를 잡자고..?"
            j "맞아. 저 말구유 앞에서 한 손은 서로의 손을 잡고, 나머지 한 손으로 저 열쇠를 잡기 위해 준비하는 거지."
            j "내가 오른손으로 네 왼손을 잡고, 우리는 나란히 선 상태에서 저 열쇠 앞에 있는 거야. 서로를 잡고 있지 않은 손은 열쇠 위로 들어올리고..."
            j "그리고 1을 외칠 때 들고 있던 그 손을 내리면서 열쇠를 쥐는 거지."
            s "...나름 룰이 있네."
            j "짧은 시간에 생각한 것 치곤 나쁘지 않지?"
            j "아무튼 내가 왼손잡이인 게 다행이지. 우리가 한 손은 서로를 잡을 수 있으니."
            s "손을 잡는 이유는..."
            j "뭐랄까 그냥 나는.. 서로를 믿고 있다는 걸 확인하는 방식이라고 생각했어. 그런 게 없으면 그 믿음이 허물어져 버릴 수 있으니.."
            j "아무튼 S, 나는 너를 믿어."
            s "그래.. 그래 나도 너를 믿어."
            j "열쇠 앞으로 가자.."

            scene bg_key
            show s normal at centerleft
            show j lonely at centerright

            "J가 말한 그대로 열쇠 앞에 섰다. 내 왼손은 J의 오른손을 잡고 있다."
            "사람의 손을 이렇게 꽉 잡은 지가 얼마인지.."
            "이런 상황에서 감상적인 소리나 하는 나도 참 어이가 없다."
            "심장이 너무 빨리 뛴다. 온 신경이 심장 소리에만 집중된 기분이다. 이러면 안돼.. 집중하자."
            "J는 나를 믿는 눈치다. 그러지 않았더라면 진작에 저 열쇠를 잡아버리지 않았을까... 아니 어쩌면 함께 탈출하기를 바라는 것일 수도 있겠다."
            "만약.. J만 나가게 된다면 아마 큰 죄책감을 느끼겠지.. 내가 그럴 수도 있듯이.."
            "함께 나갈 수만 있다면.. 그 누구도 죄책감을 느끼는 일 없이 모든 게 해결될 수 있을 거야.."
            "아...T씨는.."
            "아냐, 아냐 이미 T씨는..늦었어.. 어쩔 수 없는 거야.."

            j worry "준비됐어?"
            s surprise "자.. 잠깐만!"
            j standup "괜찮을 거야. 나는.. 우리가 함께 탈출할 수 있을 거라 믿어."
            s talk "후... 좋아 준비됐어. 나도 그렇게 믿어 J."

            "그래.. 긍정적으로 생각하자. J와 함께 이곳을 나가는 거야."

            j lonely "자.. 박자를 기억해. 3,2,1이야, 알겠지?"
            s "응 알겠어."
            j "팔 높이도 맞추고."
            s "후..."
            j "자 이제... 정말 숫자를 세자. 혹시 모르니까 5부터 세자. 자... 정말... 정말 시작한다..?"
            s "좋아. 시작해."
            j worry "5"
            "S, J" "4"
            "S, J" "3"
            show s scream
            "S, J" "2"
            show j scream
            "S, J" "1!"

            "{b}탁..{/b}" (what_size = 26)

            "S, J" "!!!"

            scene bg_hut_light with flashbulb

            "뭘까, 뭐지...? 정말 동시에 열쇠를 쥔 것 같다."
            "느낌이 이상하다. 폐 안의 공기가 다 빠져나가는 것 같아. 어질어질하다."
            "몸이.. 몸이 뜬다...? 몸이 뜬다. 동시에 졸음이 몰려온다."
            "뭐지...? 여기를 나갈 수 있게 된건가...?"
            "J가 보인다. J는..."
            "어라...? J와 이렇게 멀리 떨어져 있었던가...? 아니 잠깐만... J는 왜 몸이 뜨지 않은 거지...?"
            "졸음이 몰려온다. 수마라는 표현... 예전엔 전혀 이해 못했었는데..."
            "J의 표정이 어렴풋이 보인다.. 실망... 실망인가...? 두려움...?"
            "결국 함께 탈출할 수는 없었던 건가.. 결국 내가.. J보다 먼저 잡아서 이렇게..."
            "더 이상은 못 버틸 것 같다. J를 향해 손을 뻗고 싶다. 하지만 졸음이..."

            jump end_true_3

        "대충 둘러대다가 먼저 잡자!":
            "으아 모르겠다!!"
            "내가 살고 봐야지!!"
            "미안해...!!!"

            "J를 밀치고 열쇠를 잡았다. 급하게 잡느라 열쇠의 까슬까슬한 표면에 손바닥이 찢긴 것 같다."

            scene bg_hut_light with flashbulb
            show j scream:
                xalign 1.1
                yalign 1.0
            with dissolve
            j "ㅁ...뭐야 S! 왜 그러는 거야!"

            "J를 쳐다보지 못하겠다. 내가 이렇게...이기적인 인간일 줄이야..."
            show s give_up at center
            "그래, 나는 내가 수거하는 쓰레기만도 못한 인간이다."

            j lonely "S...?"

            "J가 황망히 서있다. 저 눈과 마주치면 평생 못 잊을 거 같아 눈을 질끈 감았다."
            "몸이 뜨기 시작한다."

            s "미안해 J, 어쩔 수 없었어...!"
            j scream "뭐야... 이게 뭐야...?!"
            s "열쇠는 한 사람만 잡을 수 있어...! 미안해!!"
            j "뭐라고...?"
            j "그럼... 그럼 나는? T씨는?! S!! {b}S!!!!!{/b}"

            "J의 외침이 들린다. 어떻게 그럴 수 있냐고 내게 소리친다."
            "미안해... 정말 미안해 J 하지만.."
            "{b}내가 살지 못하면 모든 건 의미가 없어지니까...어쩔 수 없었던 거야...{/b}"

            j tear "{b}어떻게 나한테!!{/b}" (what_size = 18)
            hide j with moveoutbottom

            "J의 목소리가 작아진다. 졸음이 몰려온다..."
            "이제 정말 이곳을 나갈 수 있는 건가...?"

            jump end_true_2

label climax_start_bad_a :
    scene white with dissolve
    play music "audio/Climax.mp3" fadeout 1

    "빛이 강하다. 눈을 뜨기가 어려워."
    "열기가 느껴진다. 여긴..."

    scene bg_desert with dissolve
    show t serious at center with dissolve
    t "아니 여긴.. 사막 아닌가요...?"
    show s surprise at centerleft with dissolve
    t "그런 것 같지는 않아요.. 아마 열쇠가 있는 곳이겠죠.."
    show j worry at centerright with dissolve
    j "열쇠를 한 사람만 잡을 수 있다는 A의 말... 도대체 무슨 의미인 걸까요...?"
    s talk "우리 중 한 사람만 이곳을 나갈 수 있다는 거 같아.."
    t "한 사람만.."
    t "이곳이 꿈의 세계가 아니라는 건 또 뭘까요...?"
    t "A씨는 이곳이 자기의 세계라고 했는데 그게 도대체.."
    j "우리가 코마 상태라고 했어."
    s "이곳을 빠져 나가지 못하면 어떻게 되는 거지..? 영원히 코마 상태인 거야..?"
    t talk "확실한 건.. 이곳을 나가지 않는 이상 현실에서 깨어날 수는 없는 거겠죠.."
    j tear "그건 안돼... 말도 안 된다고.."
    j "영원히 깨지 못하면 어떻게 해.. 그건 안돼..."

    show s normal
    "내가 뺑소니를 당했다고..."
    "코마에서 깨어나면... 예전처럼 살 수 있는 거야...?"
    "치료비는...?"
    "몸도 성치 않을 텐데.. 그럼 일도 못하겠지...? 그럼 어떻게 살아야 해...?"

    j "열쇠를 한 명만 잡을 수 있다면 어떻게 해요 우리.."

    "J는 자기가 코마에 빠졌다는 사실을 알고 더더욱 나가고 싶은 모양이다."
    "하지만 나는..."
    "코마에서 깨어난다고 하더라도... 사람 답게 살 수나 있을까..?"

    t talk "일단 걸어 가 볼까요...?"
    j standup "...좋아요."

    scene bg_desert with dissolve
    show s normal:
        xzoom -1
        xalign 1.1
        yalign 1.0
    "혼란스럽다. 뺑소니라니.."
    "어떤 모습으로 병실에 누워있을 지 상상이 되지 않는다."
    show j lonely at center with dissolve
    show t serious at left with dissolve
    "아까부터 T씨도, J도 말이 없다. 모두 표정이 어둡다. 다들 열쇠를 혼자 잡을 수 있다는 사실을 걱정하고 있는 건가...?"
    "물론 나도 그게 걱정되지만... 그것보다도.."
    "깨어나면... 코마에서 눈을 뜨면 그 다음은...?"

    t "저기 뭔가 보여요!"
    t "S씨, J씨 이 쪽이에요! 어서 오세요!"
    hide t with moveoutleft
    j "네..!"
    hide j with moveoutleft

    scene bg_sign with dissolve

    show t serious at center with dissolve
    t "여기 표지판이 있는데.. 적힌 말이.."
    show j worry at centerright with dissolve
    j "이 표지판 근방 3미터 안에 누군가가 머물러야 열쇠가 있는 오두막의 입구가 열립니다...?"
    t "한 사람은 여기 남아서 기다려야 한다는 건가 봐요."

    show s surprise at centerleft with dissolve
    s "잠...잠깐, 그렇다면.."
    j lonely "이곳에 남는 사람은.."
    j "열쇠를 잡지 못한다는 거겠지..."
    t "아..."
    s talk "하지만 누군가가 남지 않는다면.. 열쇠를 찾을 수 없게 되는 건가...?"
    j "그런 것 같아.."
    t talk "어떻게 이런.."
    s "어쩌지...? 어떻게 해...?"
    j "모르겠어.. 일단.. 생각을 좀 해보자.."

    show s normal
    "열쇠를 한 명만 잡을 수 있다면... 여기 남는 사람은 애초에 탈출할 기회조차 없는 거잖아..."
    "뭘 어떻게 하면 좋은 거야..? 누군가 포기하지 않으면 아무도 못 나가는 건가..?"
    "아니 무엇보다도 S, 너는 뭐를 원하는 거야..! 나가고 싶어? 그 인생으로 다시 돌아가고 싶어?"
    "모두가 굳은 얼굴로 입을 다물고 있다. 아마 각자의 생각에 빠진 거겠지."
    "나는 아직 정말 모르겠다. 내가 이 곳을 나가는 게 최선인지, 남는 게 최선인지..."
    "하지만 지금 당장 결정할 용기도 없어.."
    "남았다가 후회하면... 그 땐 어떻게 하냐고.."
    "사막이 이렇게 조용한 공간이었던가...? 숨이 막힌다."
    "다들 어떤 생각을 하고 있는 걸까?"

    t serious "제가.."
    s "?"
    t "제가 남을게요."
    j worry "네...?"
    show s surprise
    t "저는 어차피 이 꿈에서 깨어나도.. 제가 일했던 그 저택에 다시 돌아갈 수는 없으니까요."
    t "이전 주인님도 안 계시고.."
    t "그리고 어렴풋이 알고 있었어요. 이곳에서 나가도 아마 현실에선... 상당한 시간이 흘렀을 거라는 걸."
    t "아마 나간다고 하더라도 큰 의미가 없겠죠..."
    t "그리고... 아까 그 방에서 본 그 저택.."
    t "방 문이 닫히기 전에 정원에 다시 햇살이 비친 것 같다고 말했었죠.."
    t "그 모습이 아직 아른거려요."
    t "저는 어쩌면 예전부터 이 곳을 탈출하기보다 이 꿈 속의 저택에서 남은 시간을 보내기를 원하고 있었을지도 몰라요."
    t "아니, 그렇게 생각해요."
    t "그러니까... 제가 남을게요. 전 정말 괜찮아요."
    s talk "T씨... 정말로..."
    j "정말 괜찮으신 거예요...? 뭐라 말을 해야할지 모르겠어요.. 정말... 이 세계를 나갈 수 없어도 괜찮으세요...?"
    t normal "괜찮아요. 정말 괜찮으니까 두 분께선 어서 오두막으로 가세요."
    s "T씨..."
    j standup "정말... 고마워요 T씨.. 덕분에 이곳까지 올 수 있었어요. 정말 너무 고마워요.."
    s "고마워요... 꼭... 저택에서 행복한 시간을 보낼 수 있기를 바랄게요.. 정말 고마워요.."
    t "저는 걱정 마시고 어서 가세요..! 두 분은 부디 꼭... 기적이 일어나서 모두 이곳을 탈출하시기를 바랄게요."

    show s normal
    "함께 탈출이라... 나는 정말 이곳을 나가고 싶은 걸까...?"
    "이곳에서 나가는 게 좋은 선택인 거야...? 어쩌면 T씨가 현명한 것일 수도 있어..."

    j "그럼.. 가볼게요.. 몸 조심해요 T..!"
    s "그동안 고마웠어요..!"
    t smile "두 분도 몸 조심하세요..!"

    scene bg_desert with dissolve
    show j lonely at centerleft
    show s normal:
        xzoom -1
        xalign 0.85
        yalign 1.0
    "결국 T가 남게 되었다. J와 표지판이 가리키는 방향으로 걷는 중이다."
    "둘 다 말을 안 하고 걷기만 한 지 꽤 되었다."
    "T의 말이 머릿속에 맴돈다. 꿈 속의 저택에서 남은 시간을 보내고 싶다라..."
    "어쩌면 나에게 가장 소중했던 무언가를 보여주는 이곳에서 시간을 보내는 게 현명한 선택이 아닐까..."
    "어머니..아버지... 한 번만 더 볼 수 있다면.. 만질 수 없더라도 이야기만이라도 나눌 수 있다면..."
    "아니.. 아니지 이 세계가 언제 붕괴될 지도 모르는데... 그건 너무 위험한 선택이 아닐까..?"
    "하지만 현실에서의 내 인생은...내가 치료비를 감당이나 할 수 있을까..."

    j worry "어...? 저기 보여 S?"
    s talk "저거.. 오두막인가...?"
    j standup "우리 도착했나봐..!"
    s "드디어..."
    j "어떤 일이 생길 지 모르니까 긴장하자."

    scene bg_desert with dissolve
    show j normal at centerright
    show s normal at centerleft
    "드디어 열쇠가 있는 곳에 다다랐다."
    "이렇게 전혀 기쁨을 느끼지 못하는 상황이 될 줄은 정말 몰랐는데..."

    j "문 열어 볼게...?"
    s "그래. 같이 열자."

    scene black with dissolve
    pause .3
    scene bg_hut with dissolve

    show s surprise at centerleft with dissolve
    s "생각보다..."
    show j worry at centerright with dissolve
    j "텅 비었네..? 건초만 흩뿌려져 있고..."
    s talk "저기 저거, 말구유인가?"
    j "그런 거 같아. 다가가도 되겠지...?"
    s "조심해서 가보자."

    show s normal
    "이젠 정말 아무 것도 모르겠다."
    "누가 나 대신 선택이라도 해줬으면 좋겠다.."

    scene bg_key with dissolve
    show j lonely at centerright
    show s talk at centerleft
    j "S, 여기 열쇠가 있어."
    s "아니.. 이게 열쇠라고...? 완전 녹이 슨 고철덩이잖아...?"
    j "그래도 완전한 열쇠 모양인 걸 보니 이게 그 열쇠가 맞는 거 같아."
    s surprise "아... 드디어..."

    show j normal
    show s normal
    "우리 둘 다 기뻐하지는 않았다. J는 아마 누군가는 열쇠를 잡을 수 없다는 사실이 괴로운 거겠지.. 나도 그게 마음에 걸린다."
    "하지만... 이곳을 나가는 게 옳은 걸까...하는 생각을 떨쳐낼 수 없다."

    j "S.. 어떻게 할까...?"
    s talk "나 정말... 모르겠어."
    j standup "너무 조급하게 결정하지는 말자.. 시간을 좀 갖는 거 어때?"
    j "저기에 앉을까?"
    s normal "좋아.."

    "J의 의사는 꽤나 확고해 보인다. 내가 고민하니까 나에게 생각할 시간을 주는 거겠지.."

    scene bg_hut with dissolve
    show bg_snj_climax
    j "S, 가장 최근의 기억이 뭐야..?"
    s "무엇이 가장 최근이고 뭐가 그 전의 기억인지 잘 분간이 되지는 않아.. 하지만.."
    s "글쎄 새벽이었나..."
    s "일을 마치고 편의점에 가려 하는데 문득 그날따라 새벽이 너무 환한 거야."
    s "원래 동 트기 직전에 제일 어두운 법이거든. 이상해서 고개를 들었어."
    s "쓰레기 수거하고 차에 매달려 다니느라 온종일 고개를 처박고 있다가 일 끝나고 고개를 딱 처음 드는데."
    s "아주 큰 달이 눈에 꽉 차도록 보이는 거 있지?"
    s "달이 정말 가까이에 있었어. 처음엔 무서울 지경이었지 저게 나를 잡아가나 싶더라니까"
    s "근데 자세히 보니 아주 동그랗게 예쁜 달이 그 콧대 높은 빌딩들 보다도 더 높게, 크게 하늘에 떠 있더라."
    s "그 땐 뭔가.. 살면서 한번도 느껴보지 못한 벅참을 느낀 것 같아."
    j "그 달 나도 한 번 보고싶다."
    s "다시 없을 풍경이었어. 나만 본 게 아쉽다."
    j "나는..."

    "그 이후로도 J와 나는 서로의 단편적인 기억들을 공유했다."
    "대화를 나누다 보니 참 많은 일을 겪었구나. 그리고 그렇게 살았구나 싶었다."
    "하지만 J도 나도 알고 있었다. 더 이상 지체하면 안 된다는 사실을.."

    show j standup at center with dissolve
    j "S... 혹시 결정했어...?"

    menu :
        "J에게 양보한다":
            scene bg_key with dissolve
            show s normal at centerleft
            show j normal at centerright
            "A의 말이 자꾸만 귓가에서 아른거린다."
            "A는 내가 뺑소니를 당했다고 했다."

            j worry "S...?"

            "그렇다면 지금 내가 꿈에서 깨어나더라도, 어떤 상황일지 나는 가늠할 수가 없다."
            "팔이나 다리 중 하나가 없을 수도 있고, 최악의 경우엔..."
            "병원비도 없을 테고. 중환자실에서 상상도 못 할 고통을 경험할 지도 모르는데..."

            j "S!"
            s surprise "..어, 아 미안해!"

            "J가 나를 부르는 소리에 긴 생각에서 빠져나왔다."
            "그러고보면 J는 혼자 열쇠를 잡아버릴 수 있는데도 그러지 않았네.."
            "A에게 마음의 문을 열지 않은 나와 달리 A를 끝까지 신경 써 주기도 했고..."
            "....게다가 J는 깨어나도 찾아 갈 가족이 있겠지.."
            "....그래.. 돌아 갈 가족.."

            s talk "J, 네가 열쇠를 잡아."
            j "뭐라고?"
            s "나는 괜찮아. 나는... 이곳에 남아서 부모님을 한 번 더 뵙고싶어."
            s "나간다고 해도 나를 기다리는 건 미래가 없는 내 인생일 거고..."
            s "그러니까 J, 네가 열쇠를 잡아."
            j lonely "정말... 괜찮겠어....?"
            s "정말 괜찮아. 이 선택에 확신해. 자, 어서 이곳을 탈출해 J."
            j "그럼... 나 정말 잡는다..?"
            s "나중에 만약에.. 만약 나도 나가게 된다면 그 때 다시 보자."
            j normal "S.. 그간 고마웠어."
            j "몸 조심하고.. 잘 지내..!"
            s relax "너도!"
            j "그럼, 잡을게..!"

            "J가 열쇠를 쥔다. 아주 먼 나중에 이 선택을 후회할 수도 있겠지만..."
            "그래도 괜찮아..."

            scene bg_key_light with flashbulb

            "아.. J의 몸이 뜬다. J는 이미 눈을 감고 있다. 무슨 생각을 하고 있을까...? 아니, 잠에 든 것 같기도.."
            "나도 졸음이 몰려온다... 뭘까... 나는 이제 어떻게 되는 걸까...?"

            jump end_true_1

        "나가고 싶다고 말한다":
            if j_para >= 1:
                scene bg_key with dissolve
                show s talk at centerleft with dissolve
                s "나는.... 솔직히 나가고 싶어. 내가 너만 남기고 이 곳을 나가는 게 옳은 일인지, 이 곳을 나가서 내가 행복하게 잘 살 수 있는지 잘 모르겠지만..."
                s "그래도 이 곳을 나가고 싶어. 너에게는 미안하지만...."
                show j normal at centerright with dissolve
                j "..."

                "J가 한참동안 말이 없다. 그러다 결단을 내린 듯 나를 바라본다."

                j standup "그래, 이해해...... 자, 이 열쇠를 잡아"

                "J가 양보를 해줬다. 당장 저 열쇠를 잡으면 난 탈출할 수 있겠지....? 손만 뻗으면.... 저 열쇠를 잡으면..... 하지만 그게 쉽사리 되지 않는다."

                j "뭐해? 잡으라니까. 괜찮아"

                show s normal
                "망설이는 사이에 J가 잡으면 어떡하지? 싶은 마음도 있지만 차라리 J가 잡는 것도 나을 것 같고... 나가고 싶지만...."
                "나를 기다리던 J가 고민을 하더니 말을 꺼낸다."

                j normal "그래, 그럼."
                j "이런 식으로 정하는 건 좀 어이없겠지만, 이렇게 해보자. 3,2,1 하면 1에 우리 동시에 열쇠를 잡는거야. 둘 중 누가 나가는지는 운명에 달렸겠지."

                "그래 차라리 이런 식이라면.... 내가 나가든 J가 나가든 운명에 맡기는거야."

                s talk "좋아."
                j worry "자 그럼, 3, 2, 1!"

                "나는 열쇠를 잡았다. 그런데 열쇠를 잡은 손이 하나다."

                s surprise "J...? 왜 안 잡은거야...?"
                j standup "행복해, 외롭지 말고, 행복해."

                scene bg_hut_light with flashbulb
                "J의 목소리가 작아진다. 졸음이 몰려온다..."
                "이제 정말 이곳을 나갈 수 있는 건가...?"

                jump end_true_2

            else:
                scene bg_key with dissolve
                show s talk at centerleft with dissolve
                s "나는.... 솔직히 나가고 싶어. 내가 너만 남기고 이 곳을 나가는 게 옳은 일인지, 이 곳을 나가서 내가 행복하게 잘 살 수 있는지 잘 모르겠지만..."
                s "그래도 이 곳을 나가고 싶어."
                show j scream at centerright with dissolve
                j "나도 나가고 싶어, 너만 그런거 아니잖아! 어떻게 이렇게 끝까지 이기적이니?"
                s normal "..."

                "한참이나 나를 노려보던 나에게 J가 말을 꺼낸다."

                j lonely "그래, 그럼. 지금이라도 당장 저 열쇠를 잡고 싶지만, 그래도 여태까지 함께 한 정이 있는데 나만 나갈 수도 없지."
                j "하지만 한 명만 나갈 수 있는거라면..... 운명에 맡겨보자."
                j "말도 안될 수도 있지만 3,2,1 하면 1에 동시에 열쇠를 잡는거야. 둘 중 누가 나가는지는 운명에 달렸겠지."

                "그래, 이것도 살기 위한 경쟁이니까..."

                s talk "좋아."
                j scream "자, 3, 2, 1!"

                "나는 손을 뻗었다. J도 손을 뻗었다."

                scene bg_hut_light with flashbulb
                "내 손이 더 빨랐다."

                "J의 목소리가 작게 들려오지만.. 무슨 말을 하는 지는 알 수 없다.."
                "졸음이 몰려온다..."
                "이제 정말 이곳을 나갈 수 있는 건가...?"

                jump end_true_3
